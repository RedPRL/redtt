import prelude
import data.s2
import basics.isotoequiv
import paths.equivalence

-- a calculation of the loop space of the 2-sphere, based on
-- https://egbertrijke.com/2016/09/28/the-loop-space-of-the-2-sphere/

-- loop space of s2
data os2 where
| obase
| oloop (y : os2) (i : ğ•€) [âˆ‚[i] â†’ y]

-- I. the loop of automorphisms of os2

-- it would probably be more efficient to define this directly,
-- but we don't need it
def oloop-equiv : path (equiv os2 os2) (id-equiv/weak-connection os2) (id-equiv/weak-connection os2) =
  Î» i â†’
  ( Î» o â†’ oloop o i
  , propâ†’prop-over
    (Î» j â†’ is-equiv os2 os2 (Î» o â†’ oloop o j))
    (is-equiv/prop/direct os2 os2 (Î» o â†’ o))
    (id-equiv/weak-connection os2 .snd)
    (id-equiv/weak-connection os2 .snd)
    i
  )

-- incidentally, onegloop o is homotopic to symm (Î» i â†’ oloop o i)
def onegloop (o : os2) : path os2 o o =
  Î» i â†’ oloop-equiv i .snd o .fst .fst

def oloop-onegloop (o : os2)
  : pathd (Î» i â†’ path os2 (oloop (onegloop o i) i) o) refl refl
  =
  Î» i â†’ oloop-equiv i .snd o .fst .snd

-- II. universal cover over s2

def s2/code/surf/filler (m i j : ğ•€) : type =
  comp 0 m os2 [
  | âˆ‚[i] | j=0 â†’ ua os2 os2 (id-equiv/weak-connection os2)
  | j=1 â†’ ua os2 os2 (oloop-equiv i)
  ]

def s2/code/surf : path^1 (path^1 type os2 os2) refl refl =
  s2/code/surf/filler 1

def s2/code (a : s2) : type =
  elim a [
  | base â†’ os2
  | surf i j â†’ s2/code/surf i j
  ]

-- III. encoding function

def s2/encode (a : s2) (p : path s2 base a) : s2/code a =
  coe 0 1 obase in Î» k â†’ s2/code (p k)

-- IV. decoding function

def extend-by-surf (p : path s2 base base) (i j k : ğ•€) : s2 =
  comp 0 j (p k) [
  | âˆ‚[i] | k=0 â†’ refl
  | k=1 j â†’ surf i j
  ]

def s2/decode/base (o : os2) : path s2 base base =
  elim o [
  | obase â†’ refl
  | oloop (o' â†’ s2/decode/base/o') i â†’ extend-by-surf s2/decode/base/o' i 1
  ]

def s2/decode (a : s2) : (s2/code a) â†’ path s2 base a =
  elim a [
  | base â†’ s2/decode/base
  | surf i j â†’ Î» code k â†’
    comp 0 1 (extend-by-surf (s2/decode/base (code .cap)) i j k) [
    | âˆ‚[i k] | j=0 â†’ refl
    | j=1 m â†’ s2/decode/base (oloop-onegloop code i m) k
    ]
  ]

-- V. encode base after decode base

def s2/code/proj :
  [i j] (s2/code/surf i j â†’ os2) [
  | (âˆ‚[i] | j=1) o â†’ o
  | j=0 o â†’ oloop o i
  ]
  =
  Î» i j o â†’
  comp 0 1 (oloop (o .cap) i) [
  | (âˆ‚[i] | j=0) â†’ refl
  | j=1 â†’ oloop-onegloop o i
  ]

def s2/encode-decode/base/step (o : os2) :
  [i j] os2 [
  | âˆ‚[i] â†’ s2/encode base (s2/decode/base o)
  | j=0 â†’ oloop (s2/encode base (s2/decode/base o)) i
  | j=1 â†’ s2/encode base (s2/decode/base (oloop o i))
  ]
  =
  Î» i j â†’
  s2/code/proj i j
    (coe 0 1 obase in Î» k â†’
      s2/code (extend-by-surf (s2/decode/base o) i j k))

def s2/encode-decode/base : (o : os2) â†’ path os2 (s2/encode base (s2/decode base o)) o =
  elim [
  | obase â†’ refl
  | oloop (o' â†’ s2/encode-decode/base/o') i â†’ Î» m â†’
    comp 0 1 (oloop (s2/encode-decode/base/o' m) i) [
    | âˆ‚[i] | m=1 â†’ refl
    | m=0 j â†’ s2/encode-decode/base/step o' i j
    ]
  ]

-- VI. decode base after encode base

def s2/decode-encode/base
  (l : path s2 base base)
  : path (path s2 base base) (s2/decode base (s2/encode base l)) l
  =
  J s2 l (Î» p â†’ path (path s2 base (p 1)) (s2/decode (p 1) (s2/encode (p 1) p)) p) refl


-- VII. characterization of the loop space

def Î©1s2/equiv : equiv (path s2 base base) os2 =
  isoâ†’equiv _ _ (s2/encode base, s2/decode base, s2/encode-decode/base, s2/decode-encode/base)
