import prelude
import cool.pointed

data (X Y : ptype) âŠ¢ smash where
| basel
| baser
| proj (a : X .fst) (b : Y .fst)
| gluel (b : Y .fst) (i : ğ•€) [i=0 â†’ basel | i=1 â†’ proj (X .snd) b ]
| gluer (a : X .fst) (i : ğ•€) [i=0 â†’ baser | i=1 â†’ proj a (Y .snd) ]

def psmash (X Y : ptype) : ptype =
  (smash X Y, proj (X .snd) (Y .snd))

def smash/map (X Y Z W : ptype) (f : pmap X Z) (g : pmap Y W) : smash X Y â†’ smash Z W =
  elim [
  | basel â†’ basel
  | baser â†’ baser
  | proj a b â†’ proj (f .fst a) (g .fst b)
  | gluel b i â†’ comp 1 0 (gluel (g .fst b) i) [i=0 â†’ refl | i=1 j â†’ proj (f .snd j) (g .fst b) ]
  | gluer a i â†’ comp 1 0 (gluer (f .fst a) i) [i=0 â†’ refl | i=1 j â†’ proj (f .fst a) (g .snd j) ]
  ]

-- commutator

def commute (X Y : ptype) : smash X Y â†’ smash Y X =
  elim [
  | basel â†’ baser
  | baser â†’ basel
  | proj a b â†’ proj b a
  | gluel b i â†’ gluer b i
  | gluer a i â†’ gluel a i
  ]

def commute/involutive (X Y : ptype) : (s : smash X Y) â†’ path (smash X Y) (commute Y X (commute X Y s)) s =
  elim [* â†’ refl]

-- pivot helper functions

def pivotl/filler (X Y : ptype) (b b' : Y .fst) (j i : dim) : smash X Y =
  comp 0 j (gluel b' i) [
  | i=0 j â†’ gluel b j
  | i=1 â†’ refl
  ]

def pivotl (X Y : ptype) (b b' : Y .fst)
  : path (smash X Y) (proj (X .snd) b) (proj (X .snd) b')
  =
  pivotl/filler X Y b b' 1

def pivotr/filler (X Y : ptype) (a a' : X .fst) (j i : dim) : smash X Y =
  comp 0 j (gluer a' i) [
  | i=0 j â†’ gluer a j
  | i=1 â†’ refl
  ]

def pivotr (X Y : ptype) (a a' : X .fst)
  : path (smash X Y) (proj a (Y .snd)) (proj a' (Y .snd))
  =
  pivotr/filler X Y a a' 1

def pivot-coh (X Y : ptype)
  : path (path (smash X Y) (proj (X .snd) (Y .snd)) (proj (X .snd) (Y .snd)))
    (pivotr X Y (X .snd) (X .snd)) (pivotl X Y (Y .snd) (Y .snd))
  =
  let face (k m : dim) : smash X Y =
    comp 1 k (proj (X .snd) (Y .snd)) [
    | m=0 k â†’ gluer (X .snd) k
    | m=1 k â†’ gluel (Y .snd) k
    ]
  in
  Î» j i â†’
  comp 0 1 (face i j) [
  | i=0 k â†’ face k j
  | i=1 â†’ refl
  | j=0 k â†’ pivotr/filler X Y (X .snd) (X .snd) k i
  | j=1 k â†’ pivotl/filler X Y (Y .snd) (Y .snd) k i
  ]

def basel-baser (X Y : ptype) : path (smash X Y) basel baser =
  Î» i â†’
  comp 1 0 (gluel (Y .snd) i) [
  | i=0 â†’ refl
  | i=1 j â†’ gluer (X .snd) j
  ]

-- Definition of rearrange : (X âˆ§ Y) âˆ§ Z â†’ (Z âˆ§ Y) âˆ§ X
-- The associator can be derived from rearrange using the commutator:
-- (X âˆ§ Y) âˆ§ Z â†’ (Z âˆ§ Y) âˆ§ X â†’ (Y âˆ§ Z) âˆ§ X â†’ X âˆ§ (Y âˆ§ Z)
-- If we can show rearrange is involutive, then the associator is an equivalence

def rearrange/proj (X Y Z : ptype) (c : Z .fst) : smash X Y â†’ smash (psmash Z Y) X =
  elim [
  | basel â†’ baser
  | baser â†’ basel
  | proj a b â†’ proj (proj c b) a
  | gluel b i â†’ gluer (proj c b) i
  | gluer a i â†’
    comp 1 0 (gluel a i) [
    | i=0 â†’ refl
    | i=1 k â†’ proj (pivotr Z Y c (Z .snd) k) a
    ]
  ]

def rearrange/gluer (X Y Z : ptype) : (s : smash X Y)
  â†’ path (smash (psmash Z Y) X) basel (rearrange/proj X Y Z (Z .snd) s)
  =
  elim [
  | basel â†’ Î» i â†’ basel-baser (psmash Z Y) X i
  | baser â†’ refl
  | proj a b â†’ Î» i â†’
    comp 1 0 (gluel a i) [
    | i=0 â†’ refl
    | i=1 k â†’ proj (pivotl Z Y b (Y .snd) k) a
    ]
  | gluel b j â†’ Î» i â†’
    comp 1 0 (gluel (X .snd) i) [
    | i=0 k â†’ refl
    | i=1 k â†’
      comp 0 1 (gluer (gluel (Y .snd) k) j) [
      | j=0 m â†’ connection/and (smash (psmash Z Y) X) (Î» n â†’ gluer (proj (Z .snd) (Y .snd)) n) k m
      | j=1 m â†’ proj (pivotl/filler Z Y b (Y .snd) m k) (X .snd)
      | k=0 m â†’ gluer (gluel b m) j
      | k=1 m â†’ connection/or (smash (psmash Z Y) X) (Î» v â†’ gluer (proj (Z .snd) (Y .snd)) v) j m
      ]
    ]
  | gluer a j â†’ Î» i â†’
    comp 0 1 (connection/and (smash (psmash Z Y) X) (Î» n â†’ rearrange/proj X Y Z (Z .snd) (gluer a n)) i j) [
    | âˆ‚[i] | j=0 â†’ refl
    | j=1 k â†’
      comp 1 0 (gluel a i) [
      | i=0 â†’ refl
      | i=1 m â†’ proj (pivot-coh Z Y k m) a
      ]
    ]
  ]

def rearrange (X Y Z : ptype) : smash (psmash X Y) Z â†’ smash (psmash Z Y) X =
  elim [
  | basel â†’ baser
  | baser â†’ basel
  | proj s c â†’ rearrange/proj X Y Z c s
  | gluel c i â†’ gluer (proj c (Y .snd)) i
  | gluer s i â†’ rearrange/gluer X Y Z s i
  ]

def associate (X Y Z : ptype) (t : smash (psmash X Y) Z) : smash X (psmash Y Z) =
  commute (psmash Y Z) X
    (smash/map (psmash Z Y) X (psmash Y Z) X (commute Z Y, refl) (Î» x â†’ x, refl)
      (rearrange X Y Z t))
