import prelude.path

def connection/or
  (A : type)
  (p : ğ•€ â†’ A)
  : [i j] A [
    | j=0 | i=j â†’ p i
    | j=1 | i=1 â†’ p 1
    | i=0 â†’ p j
    ]
  =
  Î» i j â†’
  /- this is an example of something that is much nicer here than in redprl.
     we can define using line types all the faces of the composition at once.
     definitional equivalence kicks in to make this work.
  -/
  let face (l k : ğ•€) : A =
    comp 1 l (p 1) [
    | k=1 â†’ refl
    | k=0 â†’ p
    ]
  in
  comp 1 0 (p 1) [
  | i=0 â†’ face j
  | j=0 | i=j â†’ face i
  | i=1 | j=1 â†’ refl
  ]

def connection/and
  (A : type)
  (p : ğ•€ â†’ A)
  : [i j] A [
    | j=0 | i=0 â†’ p 0
    | j=1 | i=j â†’ p i
    | i=1 â†’ p j
    ]
  =
  Î» i j â†’
  let face (l k : ğ•€) : A =
    comp 0 l (p 0) [
    | k=0 â†’ refl
    | k=1 â†’ p
    ]
  in
  comp 0 1 (p 0) [
  | i=0 | j=0 â†’ refl
  | i=1 â†’ face j
  | j=1 | i=j â†’ face i
  ]

def connection/both
  (A : type)
  (p : ğ•€ â†’ A) (q : [k] A [k=0 â†’ p 1])
  : [i j] A [
    | i=0 â†’ p j
    | i=1 â†’ q j
    | j=0 â†’ p i
    | j=1 â†’ q i
    ]
  =
  Î» i j â†’
  let pface (m k : ğ•€) : A =
    comp 1 m (p 1) [
    | k=0 â†’ refl
    | k=1 â†’ p
    ]
  in
  let qface (m k : ğ•€) : A =
    comp 0 m (p 1) [
    | k=0 â†’ refl
    | k=1 â†’ q
    ]
  in
  comp 0 1 (p 1) [
  | i=0 â†’ pface j
  | i=1 â†’ qface j
  | j=0 â†’ pface i
  | j=1 â†’ qface i
  ]

def weak-connection/or
  (A : type)
  (p : ğ•€ â†’ A)
  : [i j] A [
    | i=0 â†’ p j
    | j=0 â†’ p i
    | i=1 | j=1 â†’ p 1
    ]
  =
  Î» i j â†’
  let face (l k : ğ•€) : A =
    comp 1 l (p 1) [
    | k=1 â†’ refl
    | k=0 â†’ p
    ]
  in
  comp 1 0 (p 1) [
  | i=0 â†’ face j
  | j=0 â†’ face i
  | i=1 | j=1 â†’ refl
  ]

def weak-connection/and
  (A : type)
  (p : ğ•€ â†’ A)
  : [i j] A [
    | i=0 | j=0 â†’ p 0
    | i=1 â†’ p j
    | j=1 â†’ p i
    ]
  =
  Î» i j â†’
  let face (l k : ğ•€) : A =
    comp 0 l (p 0) [
    | k=0 â†’ refl
    | k=1 â†’ p
    ]
  in
  comp 0 1 (p 0) [
  | i=0 | j=0 â†’ refl
  | i=1 â†’ face j
  | j=1 â†’ face i
  ]

def weak-connection/or-not -- i \/ ~j
  (A : type)
  (p : ğ•€ â†’ A)
  : [i j] A [
  | i=0 â†’ symm A p j
  | i=1 | j=0 â†’ p 1
  | j=1 â†’ p i
  ]
  =
  Î» i j â†’
  comp 0 1 (p 0) [
  | j=0 | i=1 â†’ p
  | j=1 k â†’ connection/and A p i k
  ]


